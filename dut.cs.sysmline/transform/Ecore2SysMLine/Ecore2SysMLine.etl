import "Ecore2SysMLine-util.eol";

pre{
//	"------------------ ETL Start ------------------".println();
	var KeyWords = baseKeyWords();
	main();
}

post{
//	"------------------ ETL Over  ------------------".println();
	("[ETL] Ecore2SysMLine >> " + SysMLine!Package.all.first().declaredName + ".sysmline").println();
}

operation main(){
	var rootEcore = Ecore!EPackage.all.first();
	var rootSysMLine = new SysMLine!Namespace;
	rootEcore.addRootNodeTo(rootSysMLine);
}

@lazy
rule Epackage
	transform s : Ecore!EPackage
	to t : SysMLine!Package{
		t.declaredName = s.name.clearKeyWord();
	}

@lazy
rule EClass
	transform s : Ecore!EClass
	to t : SysMLine!PartDefinition{
		t.declaredName = s.name.clearKeyWord();
		t.isAbstract = s.Abstract;
		if (s.eContents().getElementOf("EGenericType").size() > 0){
			for (eGenericType in s.eContents().getElementOf("EGenericType")){
				t.SubclassificationPart.add(eGenericType.eClassifier.name);
			}
		}
	}

@lazy
rule EAttribute
	transform s : Ecore!EAttribute
	to t : SysMLine!AttributeUsage{
		eStructuralFeatureTransform(s, t);
	}
	
	
@lazy
rule EReference_val
	transform s : Ecore!EReference
	to t : SysMLine!PartUsage{
		guard : s.containment==true
		eStructuralFeatureTransform(s, t);
	}

@lazy
rule EReference_ref
	transform s : Ecore!EReference
	to t : SysMLine!PartUsage{
		guard : s.containment==false
		t.isReference = true;
		eStructuralFeatureTransform(s, t);
	}
	
@lazy
rule EEnum
	transform s : Ecore!EEnum
	to t : SysMLine!EnumerationDefinition{
		t.declaredName = s.name.clearKeyWord();
	}

@lazy
rule EEnumLiteral
	transform s : Ecore!EEnumLiteral
	to t : SysMLine!EnumerationUsage{
		t.declaredName = s.name.clearKeyWord();
	}
	
@lazy
rule EDataType
	transform s : Ecore!EDataType
	to t : SysMLine!AttributeDefinition{
		t.declaredName = s.name.clearKeyWord();
	}